apply from: "$rootDir/gradleScript/lib_or_app_config.gradle"
boolean isLib(){
    return local_modules["${project.path}"]["isLib"]
}
if (isLib() /*&& project.path != ":test"*/) {
    apply plugin: 'com.android.library'
    apply from: "$rootDir/gradleScript/upload_maven.gradle"
} else {
    apply plugin: 'com.android.application'
}
def module = rootProject.ext.local_modules["${project.path}"]
android {
    namespace module["applicationId"]
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        if (!module["isLib"])
            applicationId module["applicationId"]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation rootProject.ext.androidx["androidx-appcompat"]
    implementation rootProject.ext.androidx["android.material"]
    testImplementation rootProject.ext.androidx["junit"]
    androidTestImplementation rootProject.ext.androidx["androidx.test.ext-junit"]
    androidTestImplementation rootProject.ext.androidx["androidx.test.espresso:espresso-core"]

}
